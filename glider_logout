#!/usr/bin/tcsh
## Copyright (c) 2023, 2024, 2025  University of Washington.
## 
## Redistribution and use in source and binary forms, with or without
## modification, are permitted provided that the following conditions are met:
## 
## 1. Redistributions of source code must retain the above copyright notice, this
##    list of conditions and the following disclaimer.
## 
## 2. Redistributions in binary form must reproduce the above copyright notice,
##    this list of conditions and the following disclaimer in the documentation
##    and/or other materials provided with the distribution.
## 
## 3. Neither the name of the University of Washington nor the names of its
##    contributors may be used to endorse or promote products derived from this
##    software without specific prior written permission.
## 
## THIS SOFTWARE IS PROVIDED BY THE UNIVERSITY OF WASHINGTON AND CONTRIBUTORS “AS
## IS” AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
## IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
## DISCLAIMED. IN NO EVENT SHALL THE UNIVERSITY OF WASHINGTON OR CONTRIBUTORS BE
## LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
## CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE
## GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION)
## HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT
## LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT
## OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

if(-l current) then
    cd current
endif

if(! $?PYTHON_BINARY ) then
    set PYTHON_BINARY = /opt/basestation/bin/python
endif

# A glider just logged out properly
history -T -h >>! history.log
rm -f .connected
# if running manually (not at logout) set this variable
if ( ! $?logout ) then
    setenv logout manual
endif
echo Disconnected at `date -u +"%Y-%m-%dT%H:%M:%SZ"` \(${logout}\) >>! comm.log
printf "\n" >>! comm.log
printf "\n" >>! comm.log

set runner_timeout = 2

if(! $?GLIDER_OPTIONS ) then
    set GLIDER_OPTIONS = ""
endif

if(! $?BASESTATION_PATH ) then
    set BASESTATION_PATH = "/usr/local/basestation"
endif

printf "\n" >>! baselog.log
printf "\n"  >>! baselog.log

set base_logout_time = `date -u +%y%m%d%H%M%S%3N`
set base_logout_cmd_line = "Base.py --mission_dir `pwd` --verbose --daemon --nice 10 --base_log `pwd`/baselog_`date -u +%y%m%d%H%M%S` --config `pwd`/${user}.conf ${GLIDER_OPTIONS}"
set base_logout_log = `pwd`/baselog.log

if( $?RUNNER_DIR ) then
  set base_logout_run_file = ${RUNNER_DIR}/${base_logout_time}.run
  echo ${HOME} `pwd` ${base_logout_log} ${base_logout_cmd_line} > ${base_logout_run_file}
  set t_now = `date +%s`
  while (-f ${base_logout_run_file} )
      sleep 0.2
      if ( `date +%s` >= ${t_now} + ${runner_timeout}) then
          break
      endif
  end
else
    $PYTHON_BINARY $BASESTATION_PATH/${base_logout_cmd_line}  >>&!  ${base_logout_log}
endif

